import Nat.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

VecC ◂ ★ ➔ Nat ➔ ★ = λ A : ★ . λ n : Nat . ∀ X : Nat ➔ ★ .
  X zero ➔
  (∀ n : Nat . A ➔ X n ➔ X (suc n)) ➔
  X n .

nilCV ◂ ∀ A : ★ . VecC · A zero =
  Λ A . Λ X . λ cN . λ cC . cN .

consCV ◂ ∀ A : ★ . ∀ n : Nat . A ➔ VecC · A n ➔ VecC · A (suc n) =
  Λ A . Λ n . λ x . λ xs . Λ X . λ cN . λ cC . cC -n x (xs · X cN cC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

VecP ◂ Π A : ★ . Π n : Nat . VecC · A n ➔ ★ =
  λ A : ★ . λ n : Nat . λ xsC : VecC · A n .
  ∀ X : Nat ➔ ★ . ∀ P : Π n : Nat . X n ➔ ★ .
  ∀ cN : X zero . ∀ cC : ∀ n : Nat . A ➔ X n ➔ X (suc n) .
  P zero cN ➔
  (∀ n : Nat . ∀ xs : X n . Π x : A . P n xs ➔ P (suc n) (cC -n x xs)) ➔
  P n (xsC · X cN cC) .

nilPV ◂ ∀ A : ★ . VecP · A zero (nilCV · A) = Λ A .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  pN .

consPV ◂ ∀ A : ★ . ∀ n : Nat . ∀ xsC : VecC · A n .
  Π x : A . VecP · A n xsC ➔ VecP · A (suc n) (consCV · A -n x xsC) =
  Λ A . Λ n . Λ xsC . λ x . λ xsP .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  pC -n -(xsC · X cN cC) x (xsP · X · P -cN -cC pN pC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

VecR ◂ Π A : ★ . Π n : Nat . VecC · A n ➔ ★ =
  λ A : ★ . λ n : Nat . λ xsC : VecC · A n .
  xsC · (VecC · A) nilCV consCV ≃ xsC .

nilRV ◂ ∀ A : ★ . VecR · A zero (nilCV · A) =
  Λ A . β .

consRV ◂ ∀ A : ★ . ∀ n : Nat .
  ∀ x : A . ∀ xsC : VecC · A n .
  ∀ q : VecR · A n xsC .
  VecR · A (suc n) (consCV · A -n x xsC)
  = Λ A . Λ n . Λ x . Λ xsC . Λ q .
  ρ+ q - β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Vec ◂ ★ ➔ Nat ➔ ★ = λ A : ★ . λ n : Nat .
  ι xs : (ι xsC : VecC · A n . VecP · A n xsC) . VecR · A n xs.1 .

mkVec ◂ ∀ A : ★ . ∀ n : Nat .
  Π xs : (ι xsC : VecC · A n . VecP · A n xsC) .
  VecR · A n xs.1 ➔ Vec · A n =
  Λ A . Λ n . λ xs . λ q . [ xs , ρ q - β{xs} ] .

nilV ◂ ∀ A : ★ . Vec · A zero = Λ A .
  mkVec · A -zero [ nilCV · A , nilPV · A ] (nilRV · A) .

consV ◂ ∀ A : ★ . ∀ n : Nat. A ➔ Vec · A n ➔ Vec · A (suc n) =
  Λ A . Λ n . λ x . λ xs . mkVec · A -(suc n)
  [ consCV · A -n x xs.1.1
  , consPV · A -n -xs.1.1 x xs.1.2 ]
  (consRV · A -n -x -xs.1.1 -xs.2) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

elimVec ◂ ∀ A : ★ . ∀ P : Π n : Nat . Vec · A n ➔ ★ .
  P zero (nilV · A) ➔
  (∀ n : Nat . ∀ xs : Vec · A n . Π x : A . P n xs ➔ P (suc n) (consV · A -n x xs)) ➔
  ∀ n : Nat . Π xs : Vec · A n . P n xs
  = Λ A . Λ P . λ pN . λ pC . Λ n . λ xs .
  ρ ς xs.2 -
  (xs.1.2 · (Vec · A) · P -(nilV · A) -(consV · A) pN pC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

injV ◂ ∀ A : ★ . A ➔ Vec · A (suc zero)
  = Λ A . λ x . consV · A -zero x (nilV · A) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
