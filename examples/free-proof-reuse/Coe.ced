module FreeProofReuse.Coe.
import Nat.
import List.
import Vec.
import VecL.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

v2lC' ◂ ∀ A : ★ . ∀ n : Nat . VecC · A n ➔ ListC · A
  = Λ A . Λ n . λ xs .
  xs · (λ _ : Nat . ListC · A) (nilCL · A) (Λ _ . consCL · A) .

v2lC ◂ ∀ A : ★ . ∀ n : Nat . Vec · A n ➔ ListC · A
  = Λ A . Λ n . λ xs .
  Λ X . λ cN . λ cC .
  xs.1.1 · (λ _ : Nat . X) cN (Λ _ . cC) .

v2lP ◂ ∀ A : ★ . ∀ n : Nat .
  Π xs : Vec · A n . ListP · A (v2lC · A -n xs)
  = Λ A . Λ n . λ xs .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  xs.1.2 · (λ _ : Nat . X) · (λ _ : Nat . P)
    -cN -(Λ _ . cC)
    pN (Λ _ . pC) .

v2lR ◂ ∀ A : ★ . ∀ n : Nat .
  Π xs : Vec · A n . ListR · A (v2lC · A -n xs)
  = Λ A . Λ n . λ xs . xs.2 .

v2l ◂ ∀ A : ★ . ∀ n : Nat . Vec · A n ➔ List · A
  = Λ A . Λ n . λ xs . mkList · A
  [ v2lC · A -n xs
  , v2lP · A -n xs ]
  -(v2lR · A -n xs) .
v2lId ◂ v2l ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

l2vC ◂ ∀ A : ★ . Π xs : List · A . VecC · A (length · A xs)
  = Λ A . λ xs .
  Λ X . λ cN . λ cC . elimList' · A xs ·
  (λ xs' : List · A . X (length · A xs'))
  cN
  (Λ xs' . cC -(length · A xs')) .

l2vP ◂ ∀ A : ★ . Π xs : List · A . VecP · A (length · A xs) (l2vC · A xs)
  = Λ A . λ xs .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC . elimList' · A xs ·
  (λ xs' : List · A . P (length · A xs') (l2vC · A xs' · X cN cC))
  pN
  (Λ xs' . pC -(length · A xs') -(l2vC · A xs' · X cN cC)) .

l2vR ◂ ∀ A : ★ . Π xs : List · A . VecR · A (length · A xs) (l2vC · A xs)
  = Λ A . λ xs . xs.2 .

l2v ◂ ∀ A : ★ . Π xs : List · A . Vec · A (length · A xs)
  = Λ A . λ xs . mkVec · A -(length · A xs)
  [ l2vC · A xs , l2vP · A xs ]
  -(l2vR · A xs) .
l2vId ◂ l2v ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

lengthPres ◂ ∀ A : ★ . ∀ n : Nat . Π xs : Vec · A n .
  n ≃ length · A (v2l · A -n xs)
  = Λ A . elimVec · A ·
  (λ n : Nat . λ xs : Vec · A n . n ≃ length · A (v2l · A -n xs))
  β
  (Λ n . Λ xs . λ x . λ ih . ρ ih - β) .

v2u ◂ ∀ A : ★ . ∀ n : Nat . Vec · A n ➔ VecL · A n
  = Λ A . Λ n . λ xs . mkVecL · A -n
  (v2l · A -n xs)
  -(ς (lengthPres · A -n xs)) .
v2uId ◂ v2u ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

l2u ◂ ∀ A : ★ . Π xs : List · A . VecL · A (length · A xs)
  = Λ A . λ xs . mkVecL · A -(length · A xs)
  xs
  -β .
l2uId ◂ l2u ≃ λ x . x = β .

u2l ◂ ∀ A : ★ . ∀ n : Nat . VecL · A n ➔ List · A
  = Λ A . Λ n . λ xs . xs.1 .
u2lId ◂ u2l ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

