module ListF.

import Sigma.
import IIdMapping.
import Id.

cListF ◂ ★ ➔ ★ ➔ ★ = λ A : ★. λ X : ★.  ∀ C : ★. C ➔ (A ➔ X ➔ C) ➔ C.
cin1lf ◂ ∀ A : ★. ∀ X : ★. cListF · A · X = Λ A. Λ X. Λ C. λ c. λ f. c.
cin2lf ◂ ∀ A : ★. ∀ X : ★. A ➔ X ➔ cListF · A · X = Λ A. Λ X. λ a. λ x. Λ C. λ c. λ f. f a x  .


cListFInd ◂ Π A : ★. Π X : ★. cListF · A · X ➔ ★ 
 = λ A : ★. λ X :  ★. λ lf : cListF · A · X. ∀ Q : cListF · A · X ➔ ★. 
 Q (cin1lf · A · X) ➔ (Π a : A . Π x : X. Q (cin2lf · A · X a x)) ➔ Q lf.


-- "inductive" ListF with its dependent elimination
ListF ◂ ★ ➔ ★ ➔ ★ = λ A : ★. λ X : ★. ι x : cListF · A · X. cListFInd · A · X x.
in1lf ◂ ∀ A : ★. ∀ X : ★. ListF · A · X = Λ A. Λ X. [ Λ C. λ c. λ f. c , Λ A. λ c1. λ c2. c1 ] .
in2lf ◂ ∀ A : ★. ∀ X : ★. A ➔ X ➔ ListF · A · X = Λ A. Λ X. λ a. λ x. [ Λ C. λ c. λ f. f a x , Λ C. λ c. λ f. f a x ] .


ListFInd ◂ ∀ A : ★. ∀ X : ★. Π lf : ListF · A · X . ∀ Q : ListF · A · X ➔ ★. 
 (Π a : A . Π x : X. Q (in2lf · A · X a x)) ➔ Q (in1lf · A · X) ➔ Q lf = Λ A. Λ X. λ lf. Λ Q. λ i2. λ i1.   
 lf.2 · (λ x : cListF · A · X. ∀ Y : ★ . (Π x' : ListF · A · X  . {x ≃ x'} ➔ Q x' ➔ Y ) ➔ Y) 
 (Λ Y. λ q. q (in1lf · A · X) β i1) (λ a. λ x. Λ Y. λ q. q (in2lf · A · X a x) β (i2 a x) ) · (Q lf) 
 (λ x' . λ e . λ u . ρ e - u).


-- identity mapping for ListF
lfimap ◂ ∀ A : ★. IdMapping · (ListF · A) 
 = Λ A. Λ X. Λ Y. Λ c.  intrIdExt · (ListF · A · X) · (ListF · A · Y)
 (λ lf. lf.1 · (ListF · A · Y) (in1lf · A · Y) 
   (λ a. λ x. in2lf · A · Y a (elimIdExt · X · Y -c  x))) 
 (λ z. θ<z> (ListFInd · A · X z) (λ a. λ x. β) β).
