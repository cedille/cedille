module Cast.

import SubstComp.
import Id.


Cast ◂ ★ ➔ ★ ➔ ★ =
  λ A : ★ . λ B : ★ .
  Π a : A . ι b : B . b ≃ a .


elimCast ◂ ∀ A : ★ . ∀ B : ★ . Cast · A · B ➾ Π a : A . B =
  Λ A . Λ B . Λ c . λ a . subst · B · A -(c a).1 a -(c a).2.

intrCast ◂ ∀ X : ★. ∀ Y : ★. Π f : X ➔ Y. (Π z : X. (f z ≃ z)) ➔ Cast · X · Y 
 = Λ X. Λ Y. λ if. λ p.  λ a. [ if a , ρ (p a) - β{if a} ].


c2i ◂ ∀ X : ★. ∀ Y : ★. Cast · X · Y ➾ Id · X · Y 
 = Λ X. Λ Y. Λ c. intrId · X · Y (elimCast · X · Y -c) (λ z. β)  .


i2c ◂ ∀ X : ★. ∀ Y : ★. Id · X · Y  ➾ Cast · X · Y 
 = Λ X. Λ Y. Λ c. intrCast · X · Y (elimId · X · Y -c) (λ z. β)  .


CastDep ◂ Π A : ★.  (A ➔ ★) ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ .
  Π a : A . ι b : B a . b ≃ a .


elimCastDep ◂ ∀ A : ★ . ∀ B : A ➔ ★ . CastDep · A · B ➾ Π a : A . B a =
  Λ A . Λ B . Λ c . λ a . subst · (B a) · A -(c a).1 a -(c a).2.
