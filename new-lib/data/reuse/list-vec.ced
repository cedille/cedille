module data.reuse.list-vec.

import ../nat.
import ../list.
import ../vec.

import ../../core/cast.
import ../../core/castDep.

vec2ListId : ∀ A: ★. ∀ n: Nat. Cast ·(Vec ·A n) ·(List ·A)
= Λ A.
  [f : ∀ n: Nat. Vec ·A n ➔ List ·A
    = Λ n. λ xs. μ rec. xs {
      | vnil ➔ nil ·A
      | vcons -n x xs ➔ cons x (rec -n xs)
      }]
- [pf : ∀ n: Nat. Π xs: Vec ·A n. {f xs ≃ xs}
    = Λ n. λ xs. μ ih. xs {
      | vnil ➔ β
      | vcons -n x xs ➔
        ρ (ih -n xs) @ z. {cons x z ≃ vcons x xs} - β
      }]
- Λ n. intrCast -(f -n) -(pf -n).

vec2List! : ∀ A: ★. ∀ n: Nat. Vec ·A n ➔ List ·A
= Λ A. Λ n. elimCast -(vec2ListId ·A -n).

_ : {vec2List! ≃ λ x. x} = β.

list2VecId : ∀ A: ★. CastDep ·(List ·A) ·(λ xs: List ·A. Vec ·A (length xs))
= Λ A.
  [f : Π xs: List ·A. Vec ·A (length xs)
    = λ xs. μ rec. xs {
      | nil ➔ vnil ·A
      | cons x xs ➔
        [xs' = to/List -isType/rec xs]
      - vcons -(length xs') x (rec xs)
      }]
- [pf : Π xs: List ·A. {f xs ≃ xs}
    = λ xs. μ ih. xs {
      | nil ➔ β
      | cons x xs ➔
        ρ (ih xs) @ z. {cons x z ≃ cons x xs}
      - β
      }]
- intrCastDep -f -pf.

list2Vec! : ∀ A: ★. Π xs: List ·A. Vec ·A (length xs)
= Λ A. elimCastDep -(list2VecId ·A).

_ : {list2Vec! ≃ λ x. x} = β.

reuse1V2L : ∀ A: ★. ∀ g: (∀ n: Nat. Π xs: Vec ·A n. Nat). Π f: (∀ n: Nat. Π xs: Vec ·A n. Vec ·A (g -n xs)). List ·A ➔ List ·A
= Λ A. Λ g. λ f. λ xs.
  [xs' = list2Vec! xs]
  - [n = length xs]
  - vec2List! -(g -n xs') (f -n xs').

_ : { reuse1V2L ≃ λ x. x } = β .

reuse1L2V : ∀ A: ★. Π g: (List ·A ➔ List ·A). ∀ n: Nat. Π xs: Vec ·A n. Vec ·A (length (g (vec2List! -n xs)))
= Λ A. λ g. Λ n. λ xs. list2Vec! (g (vec2List! -n xs)).

_ : { reuse1L2V ≃ λ g. g } = β .

