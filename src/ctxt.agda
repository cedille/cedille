module ctxt where

open import lib
open import cedille-types
open import syntax-util
open import to-string

location : Set
location = string Ã— posinfo -- file path and starting position in the file 

{- we will generally keep classifiers of variables in hnf in the ctxt, although
   we will not necessarily unfold recursive type definitions. -}

data ctxt-info : Set where

  -- for declaring a variable to have a given type (with no definition)
  term-decl : type â†’ ctxt-info

  -- for defining a variable to equal a term with a given type
  term-def : term â†’ type â†’ ctxt-info

  -- for untyped term definitions (used only when checking recursive datatype definitions)
  term-udef : term â†’ ctxt-info

  -- for declaring a variable to have a given kind (with no definition)
  type-decl : kind â†’ ctxt-info

  -- for defining a variable to equal a type with a given kind
  type-def : type â†’ kind â†’ ctxt-info

  -- for defining a variable to equal a type, without a kind
  type-udef : type â†’ ctxt-info

  -- for defining a variable to equal a kind
  kind-def : kind â†’ ctxt-info

  -- to rename a variable at any level to another
  rename-def : var â†’ ctxt-info

  -- for a recursive type definition
  rec-def : type â†’ kind â†’ ctxt-info

  -- representing a declaration of a variable with no other information about it
  var-decl : ctxt-info

data ctxt : Set where
  mk-ctxt : (current-file : string) â†’ trie (ctxt-info Ã— location) â†’ ctxt

new-ctxt : (current-file : string) â†’ ctxt
new-ctxt file-name = mk-ctxt file-name empty-trie

ctxt-term-decl : posinfo â†’ var â†’ type â†’ ctxt â†’ ctxt
ctxt-term-decl p v t (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (term-decl t , (cur-file , p)))

ctxt-type-decl : posinfo â†’ var â†’ kind â†’ ctxt â†’ ctxt
ctxt-type-decl p v k (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (type-decl k , (cur-file , p)))

ctxt-type-def : posinfo â†’ var â†’ type â†’ kind â†’ ctxt â†’ ctxt
ctxt-type-def p v t k (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (type-def t k , (cur-file , p)))

ctxt-kind-def : posinfo â†’ var â†’ kind â†’ ctxt â†’ ctxt
ctxt-kind-def p v k (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (kind-def k , (cur-file , p)))

ctxt-type-udef : posinfo â†’ var â†’ type â†’ ctxt â†’ ctxt
ctxt-type-udef p v t (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (type-udef t , (cur-file , p)))

ctxt-term-def : posinfo â†’ var â†’ term â†’ type â†’ ctxt â†’ ctxt
ctxt-term-def p v t tp (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (term-def t tp , (cur-file , p)))

ctxt-term-udef : posinfo â†’ var â†’ term â†’ ctxt â†’ ctxt
ctxt-term-udef p v t (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (term-udef t , (cur-file , p)))

ctxt-var-decl : posinfo â†’ var â†’ ctxt â†’ ctxt
ctxt-var-decl p v (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (var-decl , (cur-file , p)))

ctxt-var-decl-if : posinfo â†’ var â†’ ctxt â†’ ctxt
ctxt-var-decl-if p v (mk-ctxt cur-file i) = 
  if trie-contains i v then (mk-ctxt cur-file i) else (mk-ctxt cur-file (trie-insert i v (var-decl , (cur-file , p))))

ctxt-rename-rep : ctxt â†’ var â†’ var
ctxt-rename-rep (mk-ctxt cur-file i) v with trie-lookup i v 
ctxt-rename-rep (mk-ctxt cur-file i) v | just (rename-def v' , _) = v'
ctxt-rename-rep (mk-ctxt cur-file i) v | _ = v

-- we assume that only the left variable might have been renamed
ctxt-eq-rep : ctxt â†’ var â†’ var â†’ ğ”¹
ctxt-eq-rep Î“ x y = (ctxt-rename-rep Î“ x) =string y

{- add a renaming mapping the first variable to the second, unless they are equal.
   Notice that adding a renaming for v will overwrite any other declarations for v. -}
ctxt-rename : posinfo â†’ var â†’ var â†’ ctxt â†’ ctxt
ctxt-rename p v v' (mk-ctxt cur-file i) = (mk-ctxt cur-file (trie-insert i v (rename-def v' , (cur-file , p))))

ctxt-tk-decl : posinfo â†’ var â†’ tk â†’ ctxt â†’ ctxt
ctxt-tk-decl p x (Tkt t) Î“ = ctxt-term-decl p x t Î“ 
ctxt-tk-decl p x (Tkk k) Î“ = ctxt-type-decl p x k Î“ 

ctxt-tk-def : posinfo â†’ var â†’ var â†’ tk â†’ ctxt â†’ ctxt
ctxt-tk-def p x y (Tkt t) Î“ = ctxt-term-def p x (Var posinfo-gen y) t Î“ 
ctxt-tk-def p x y (Tkk k) Î“ = ctxt-type-def p x (TpVar posinfo-gen y) k Î“ 

ctxt-rec-def : posinfo â†’ var â†’ type â†’ kind â†’ ctxt â†’ ctxt
ctxt-rec-def p v t k (mk-ctxt cur-file i) = mk-ctxt cur-file (trie-insert i v (rec-def t k , (cur-file , p)))

ctxt-binding-to-string : string Ã— (ctxt-info Ã— location) â†’ string
ctxt-binding-to-string (x , term-decl tp , _) = "term " ^ x ^ " : " ^ type-to-string tp 
ctxt-binding-to-string (x , term-def t tp , _) = "term " ^ x ^ " = " ^ term-to-string t ^ " : " ^ type-to-string tp 
ctxt-binding-to-string (x , term-udef t , _) = "term " ^ x ^ " = " ^ term-to-string t 
ctxt-binding-to-string (x , type-decl k , _) = "type " ^ x ^ " : " ^ kind-to-string k 
ctxt-binding-to-string (x , type-def tp k , _) = "type " ^ x ^ " = " ^ type-to-string tp ^ " : " ^ kind-to-string k 
ctxt-binding-to-string (x , type-udef tp , _) = "type " ^ x ^ " = " ^ type-to-string tp
ctxt-binding-to-string (x , kind-def k , _) = "type " ^ x ^ " = " ^ kind-to-string k 
ctxt-binding-to-string (x , rename-def y , _) = "rename " ^ x ^ " to " ^ y 
ctxt-binding-to-string (x , rec-def tp k , _) = "rec " ^ x ^ " = " ^ type-to-string tp ^ " : " ^ kind-to-string k 
ctxt-binding-to-string (x , var-decl , _) = "expr " ^ x

ctxt-to-string : ctxt â†’ string
ctxt-to-string (mk-ctxt cur-file i) = "[" ^ (string-concat-sep-map " | " ctxt-binding-to-string (trie-mappings i)) ^ "]"

local-ctxt-to-string : ctxt â†’ string
local-ctxt-to-string (mk-ctxt cur-file i) = "[" ^ (string-concat-sep-map " | " ctxt-binding-to-string (filter helper (trie-mappings i))) ^ "]"
  where helper : string Ã— ctxt-info Ã— location â†’ ğ”¹
        helper (_ , term-decl _ , _) = tt
        helper (_ , type-decl _ , _) = tt
        helper _ = ff

----------------------------------------------------------------------
-- lookup functions
----------------------------------------------------------------------

-- look for a kind for the given var, which is assumed to be a type
ctxt-lookup-type-var : ctxt â†’ var â†’ maybe kind
ctxt-lookup-type-var (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-type-var (mk-ctxt _ i) v | just (type-decl k , _) = just k
ctxt-lookup-type-var (mk-ctxt _ i) v | just (type-def _ k , _) = just k
ctxt-lookup-type-var (mk-ctxt _ i) v | just (rec-def _ k , _) = just k
ctxt-lookup-type-var (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-term-var : ctxt â†’ var â†’ maybe type
ctxt-lookup-term-var (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-term-var (mk-ctxt _ i) v | just (term-decl t , _) = just t
ctxt-lookup-term-var (mk-ctxt _ i) v | just (term-def _ t , _) = just t
ctxt-lookup-term-var (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-var-tk : ctxt â†’ var â†’ maybe tk
ctxt-lookup-var-tk (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-var-tk (mk-ctxt _ i) v | just (type-decl k , _) = just (Tkk k)
ctxt-lookup-var-tk (mk-ctxt _ i) v | just (type-def _ k , _) = just (Tkk k)
ctxt-lookup-var-tk (mk-ctxt _ i) v | just (term-decl t , _) = just (Tkt t)
ctxt-lookup-var-tk (mk-ctxt _ i) v | just (term-def _ t , _) = just (Tkt t)
ctxt-lookup-var-tk (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-kind-var : ctxt â†’ var â†’ ğ”¹
ctxt-lookup-kind-var (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-kind-var (mk-ctxt _ i) v | just (kind-def _ , _) = tt
ctxt-lookup-kind-var (mk-ctxt _ i) v | _ = ff

ctxt-lookup-term-var-def : ctxt â†’ var â†’ maybe term
ctxt-lookup-term-var-def (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-term-var-def (mk-ctxt _ i) v | just (term-def t _ , _) = just t
ctxt-lookup-term-var-def (mk-ctxt _ i) v | just (term-udef t , _) = just t
ctxt-lookup-term-var-def (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-type-var-def : ctxt â†’ var â†’ maybe type
ctxt-lookup-type-var-def (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-type-var-def (mk-ctxt _ i) v | just (type-def t _ , _) = just t
ctxt-lookup-type-var-def (mk-ctxt _ i) v | just (type-udef t , _) = just t
ctxt-lookup-type-var-def (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-type-var-rec-def : ctxt â†’ var â†’ maybe type
ctxt-lookup-type-var-rec-def (mk-ctxt _ i) v with trie-lookup i v
ctxt-lookup-type-var-rec-def (mk-ctxt _ i) v | just (rec-def t _ , _) = just t
ctxt-lookup-type-var-rec-def (mk-ctxt _ i) v | _ = nothing

ctxt-lookup-kind-var-def : ctxt â†’ var â†’ maybe kind
ctxt-lookup-kind-var-def (mk-ctxt _ i) x with trie-lookup i x
ctxt-lookup-kind-var-def (mk-ctxt _ i) x | just (kind-def k , _) = just k
ctxt-lookup-kind-var-def (mk-ctxt _ i) x | _ = nothing

ctxt-binds-var : ctxt â†’ var â†’ ğ”¹
ctxt-binds-var (mk-ctxt _ i) x = trie-contains i x

ctxt-var-location : ctxt â†’ var â†’ location
ctxt-var-location (mk-ctxt _ i) x with trie-lookup i x
ctxt-var-location (mk-ctxt _ i) x | just (_ , l) = l
ctxt-var-location (mk-ctxt _ i) x | nothing = "missing" , "missing"

ctxt-set-current-file : ctxt â†’ string â†’ ctxt
ctxt-set-current-file (mk-ctxt _ i) file-name = mk-ctxt file-name i

ctxt-get-current-file : ctxt â†’ string
ctxt-get-current-file (mk-ctxt filename i) = filename
