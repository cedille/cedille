module kripke-semantics where

open import level
open import bool
open import closures
open import empty
open import eq
open import level
open import list
open import list-thms
open import nat
open import product
open import relations
open import string
open import sum
open import unit

data formula : Set where
  $ : string â†’ formula
  True : formula
  Implies : formula â†’ formula â†’ formula
  And : formula â†’ formula â†’ formula

ctxt : Set
ctxt = ğ•ƒ formula

data _âŠ¢_ : ctxt â†’ formula â†’ Set where
  Assume : âˆ€{Î“ f} â†’ (f :: Î“) âŠ¢ f
  Weaken : âˆ€{Î“ f f'} â†’ Î“ âŠ¢ f â†’ (f' :: Î“) âŠ¢ f
  ImpliesI : âˆ€{f1 f2 Î“} â†’ (f1 :: Î“) âŠ¢ f2 â†’ Î“ âŠ¢ (Implies f1 f2)
  ImpliesE : âˆ€{f1 f2 Î“} â†’ Î“ âŠ¢ (Implies f1 f2) â†’ Î“ âŠ¢ f1 â†’ Î“ âŠ¢ f2
  TrueI : âˆ€ {Î“} â†’ Î“ âŠ¢ True
  AndI : âˆ€{f1 f2 Î“} â†’ Î“ âŠ¢ f1 â†’ Î“ âŠ¢ f2 â†’ Î“ âŠ¢ (And f1 f2)
  AndE : âˆ€(b : ğ”¹){f1 f2 Î“} â†’ Î“ âŠ¢ (And f1 f2) â†’ Î“ âŠ¢ (if b then f1 else f2)

sample-pf : [] âŠ¢ Implies ($ "p") (And ($ "p") ($ "p"))
sample-pf = ImpliesI{$ "p"} (AndI (Assume{[]}) (Assume))

record struct : Set1 where
  field W         : Set -- a set of worlds
        R         : W â†’ W â†’ Set
        preorderR : preorder R -- a proof that R is a preorder (reflexive and transitive)
        V         : W â†’ string â†’ Set -- a valuation telling whether atomic formula i is true or false in a given world
        monoV     : âˆ€ { w w' } â†’ R w w' â†’ âˆ€ { i } â†’ V w i â†’ V w' i
  reflR : reflexive R
  reflR = fst preorderR
  transR : transitive R
  transR = snd preorderR

open struct

_,_âŠ¨_ : âˆ€(k : struct) â†’ W k â†’ formula â†’ Set
k , w âŠ¨ ($ x) = V k w x
k , w âŠ¨ True = âŠ¤
k , w âŠ¨ Implies f1 f2 = âˆ€ {w' : W k} â†’ R k w w' â†’ k , w' âŠ¨ f1 â†’ k , w' âŠ¨ f2
k , w âŠ¨ And f1 f2 = k , w âŠ¨ f1 âˆ§ k , w âŠ¨ f2

module âŠ¨-example where
  data world : Set where
    w0 : world
    w1 : world
    w2 : world

  data rel : world â†’ world â†’ Set where
    r00 : rel w0 w0 
    r11 : rel w1 w1 
    r22 : rel w2 w2 
    r01 : rel w0 w1 
    r02 : rel w0 w2 

  rel-refl : reflexive rel
  rel-refl {w0} = r00
  rel-refl {w1} = r11 
  rel-refl {w2} = r22

  rel-trans : transitive rel
  rel-trans r00 r00 = r00
  rel-trans r00 r01 = r01
  rel-trans r00 r02 = r02
  rel-trans r11 r11 = r11
  rel-trans r22 r22 = r22
  rel-trans r01 r11 = r01
  rel-trans r02 r22 = r02

  data val : world â†’ string â†’ Set where
    v1p : val w1 "p" 
    v1q : val w1 "q" 
    v2p : val w2 "p" 
    v2q : val w2 "q" 

  mono-val : âˆ€{w w'} â†’ rel w w' â†’ âˆ€ { i } â†’ val w i â†’ val w' i
  mono-val r00 p = p
  mono-val r11 p = p
  mono-val r22 p = p
  mono-val r01 ()
  mono-val r02 ()

  k : struct
  k = record { W = world ; R = rel ; preorderR = (rel-refl , rel-trans) ; V = val ; monoV = mono-val }

  test-sem : Set
  test-sem = k , w0 âŠ¨ Implies ($ "p") ($ "q")

  pf-test-sem : k , w0 âŠ¨ Implies ($ "p") ($ "q")
  pf-test-sem r00 ()
  pf-test-sem r01 p = v1q
  pf-test-sem r02 p = v2q

monoâŠ¨ : âˆ€{k : struct}{w1 w2 : W k}{f : formula} â†’ 
         R k w1 w2 â†’ 
         k , w1 âŠ¨ f â†’ 
         k , w2 âŠ¨ f
monoâŠ¨{k} {f = $ x} r p = monoV k r p 
monoâŠ¨{k} {f = True} r p = triv
monoâŠ¨{k} {f = Implies f1 f2} r p r' p' = p (transR k r r') p'
monoâŠ¨{k} {f = And f1 f2} r (p1 , p2) = monoâŠ¨{f = f1} r p1 , monoâŠ¨{f = f2} r p2

_,_âŠ¨ctxt_ : âˆ€(k : struct) â†’ W k â†’ ctxt â†’ Set
k , w âŠ¨ctxt [] = âŠ¤ 
k , w âŠ¨ctxt (f :: Î“) = (k , w âŠ¨ f) âˆ§ (k , w âŠ¨ctxt Î“)

monoâŠ¨ctxt : âˆ€{k : struct}{Î“ : ctxt}{w1 w2 : W k} â†’ 
            R k w1 w2 â†’ 
            k , w1 âŠ¨ctxt Î“ â†’ 
            k , w2 âŠ¨ctxt Î“
monoâŠ¨ctxt{k}{[]} _ _ = triv
monoâŠ¨ctxt{k}{f :: Î“} r (u , v) = monoâŠ¨{k}{f = f} r u , monoâŠ¨ctxt{k}{Î“} r v

_âŠ©_ : ctxt â†’ formula â†’ Set1
Î“ âŠ© f = âˆ€{k : struct}{w : W k} â†’ k , w âŠ¨ctxt Î“ â†’ k , w âŠ¨ f

Soundness : âˆ€{Î“ : ctxt}{f : formula} â†’ Î“ âŠ¢ f â†’ Î“ âŠ© f
Soundness Assume g = fst g
Soundness (Weaken p) g = Soundness p (snd g)
Soundness (ImpliesI p) g r u' = Soundness p (u' , monoâŠ¨ctxt r g)
Soundness (ImpliesE p p') {k} g = (Soundness p g) (reflR k) (Soundness p' g)
Soundness TrueI g = triv
Soundness (AndI p p') g = (Soundness p g , Soundness p' g)
Soundness (AndE tt p) g = fst (Soundness p g)
Soundness (AndE ff p) g = snd (Soundness p g)

data _â‰¼_ : ğ•ƒ formula â†’ ğ•ƒ formula â†’ Set where 
  â‰¼-refl : âˆ€ {Î“} â†’ Î“ â‰¼ Î“
  â‰¼-cons : âˆ€ {Î“ Î“' f} â†’ Î“ â‰¼ Î“' â†’ Î“ â‰¼ (f :: Î“')
    
â‰¼-trans : âˆ€ {Î“ Î“' Î“''} â†’ Î“ â‰¼ Î“' â†’ Î“' â‰¼ Î“'' â†’ Î“ â‰¼ Î“''
â‰¼-trans u â‰¼-refl = u
â‰¼-trans u (â‰¼-cons u') = â‰¼-cons (â‰¼-trans u u') 

Weakenâ‰¼ : âˆ€ {Î“ Î“'}{f : formula} â†’ Î“ â‰¼ Î“' â†’ Î“ âŠ¢ f â†’ Î“' âŠ¢ f
Weakenâ‰¼ â‰¼-refl p = p
Weakenâ‰¼ (â‰¼-cons d) p = Weaken (Weakenâ‰¼ d p)

U : struct
U = record { W = ctxt ;
             R = _â‰¼_ ;
             preorderR = â‰¼-refl , â‰¼-trans ;
             V = Î» Î“ n â†’ Î“ âŠ¢ $ n ;
             monoV = Î» d p â†’ Weakenâ‰¼ d p }

CompletenessU : âˆ€{f : formula}{Î“ : W U} â†’ U , Î“ âŠ¨ f â†’ Î“ âŠ¢ f 
SoundnessU : âˆ€{f : formula}{Î“ : W U} â†’ Î“ âŠ¢ f â†’ U , Î“ âŠ¨ f
CompletenessU {$ x} u = u
CompletenessU {True} u = TrueI
CompletenessU {And f f'} u = AndI (CompletenessU{f} (fst u)) (CompletenessU{f'} (snd u))
CompletenessU {Implies f f'}{Î“} u = 
  ImpliesI 
    (CompletenessU {f'} 
      (u (â‰¼-cons â‰¼-refl) (SoundnessU {f} (Assume {Î“}))))
SoundnessU {$ x} p = p
SoundnessU {True} p = triv
SoundnessU {And f f'} p = SoundnessU{f} (AndE tt p) , SoundnessU{f'} (AndE ff p)
SoundnessU {Implies f f'} p r u = SoundnessU (ImpliesE (Weakenâ‰¼ r p) (CompletenessU {f} u))

ctxt-id : âˆ€{Î“ : ctxt} â†’ U , Î“ âŠ¨ctxt Î“
ctxt-id{[]} = triv
ctxt-id{f :: Î“} = SoundnessU{f} Assume , monoâŠ¨ctxt (â‰¼-cons â‰¼-refl) (ctxt-id {Î“}) 

Completeness : âˆ€{Î“ : ctxt}{f : formula} â†’ Î“ âŠ© f â†’ Î“ âŠ¢ f 
Completeness{Î“} p = CompletenessU (p{U}{Î“} (ctxt-id{Î“}))

Universality1 : âˆ€{Î“ : ctxt}{f : formula} â†’ Î“ âŠ© f â†’ U , Î“ âŠ¨ f
Universality1{Î“}{f} p = SoundnessU (Completeness{Î“}{f} p)

Universality2 : âˆ€{Î“ : ctxt}{f : formula} â†’ U , Î“ âŠ¨ f â†’ Î“ âŠ© f
Universality2{Î“}{f} p = Soundness (CompletenessU{f}{Î“} p)

nbe : âˆ€ {Î“ f} â†’ Î“ âŠ¢ f â†’ Î“ âŠ¢ f
nbe {Î“} p = Completeness (Soundness p)

module tests where

  -- here we see several proofs which normalize to just TrueI using the nbe function

  a : [] âŠ¢ True
  a = AndE tt (AndI TrueI TrueI)
  a' = nbe a

  b : [] âŠ¢ True
  b = ImpliesE (ImpliesE (ImpliesI (ImpliesI (Assume))) TrueI) TrueI
  b' = nbe b

  c : [] âŠ¢ (Implies ($ "p") ($ "p"))
  c = ImpliesI (ImpliesE (ImpliesI Assume) Assume)
  c' = nbe c

  d : [ $ "q" ] âŠ¢ (Implies ($ "p") ($ "q"))
  d = ImpliesI (ImpliesE (ImpliesI (Weaken (Weaken Assume))) Assume)
  d' = nbe d

  e : [] âŠ¢ (Implies (And ($ "p") ($ "q")) (And ($ "p") ($ "q")))
  e = ImpliesI Assume
  e' = nbe e

  f : [] âŠ¢ (Implies (Implies ($ "p") ($ "q")) (Implies ($ "p") ($ "q")))
  f = ImpliesI Assume
  f' = nbe f

